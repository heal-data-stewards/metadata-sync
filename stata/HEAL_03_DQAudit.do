/* -------------------------------------------------------------------------------- */
/* Project: HEAL 																	*/
/* PI: Kira Bradford																*/
/* Program: HEAL_03_DQAudit															*/
/* Programmer: Sabrina McCutchan (CDMS)												*/
/* Date Created: 2025/03/27															*/
/* Date Last Updated: 2026/01/30													*/
/* Description:	This program performs a data quality audit checking the completeness*/	
/*	of data about NIH awards related to HEAL studies in the MySQL database.			*/			
/*		1. Create key of appl_ids in MySQL  										*/
/*		2. Query NIH Reporter for all awards sharing a project serial number with 	*/
/*			an award in MySQL 														*/
/*		3. Compile NIH Reporter query output & existing awards table				*/
/*		4. Flag appls that are in the reporter table								*/
/*		5. Isolate awards we should track that aren't yet in MySQL   				*/
/*		6. Rename vars to same names used in reporter table							*/
/*		7. Format and output table for MySQL										*/
/*		8. Debug: split up large file into 2 for MySQL import						*/
/*																					*/
/* Notes:  																			*/
/*		- Last run 2026/01/15														*/
/*		- See https://docs.google.com/document/d/									*/
/*		  11OOFOEzp_2ZDSM6kXE8G6Lpxs_qTmKDX92F0L5kh9z0/edit?tab=t.0  for a write-up */
/*		  of why and how this DQ audit was performed.								*/
/*																					*/
/* -------------------------------------------------------------------------------- */


clear



/* ----- 1. Create key of appl_ids in MySQL ----- */

use "$temp/nihtables_$today.dta", clear
keep appl_id heal_funded proj_ser_num
merge 1:1 appl_id using "$raw/research_networks_$today.dta"
drop if _merge==2
duplicates drop
gen in_resnet=0
replace in_resnet=1 if strtrim(res_net)!=""
keep appl_id heal_funded res_net in_resnet proj_ser_num
rename proj_ser_num in_rep_table_sernum
save "$temp/mysql_applkey.dta", replace /*n=2439*/



/* ----- 2. Query NIH Reporter for all awards sharing a project serial number with an award in MySQL ----- */
/* Note: This process is done outside of MySQL. The file "$raw/xold/heal_awards_by_serial_number_02142025.csv" (called below) was generated by Maria Davila. She took every unique project serial number that existed in the MySQL reporter and awards tables (these table n's were identical at time of query) and fetched every appl_id associated with those serial numbers from NIH Reporter. */
/* Note 2: On 2026/01/15, attempted to rerun this process with extract file "$raw/heal_awards_reporter_01022025_sn.csv". These are *additional* records that should be added to the first DQ audit run, not a replacement for the prior file. The extract file had data quality issues: there were 22 duplicates of each row, and not every serial number sent in the query was represented in the output file. */
/* Note 3: On 2026/01/26, attempted to rerun this process with another extract file "$raw/raw source data/heal_awards_reporter_01152025_sn.csv" */



/* ----- 3. Compile NIH Reporter query output & existing awards table ----- */
/*
* Import full list of candidates from NIH Reporter - Feb 2025 *;
import delim using "$raw/xold/heal_awards_by_serial_number_02142025.csv", delimiters(",") varnames(1) bindquotes(strict) case(preserve) maxquotedrows(30) stringcols(_all) clear
capture drop heal_funded
duplicates drop /*n=4,479*/
gen dqauditwave=1
save "$raw/heal_awards_by_serial_number_02142025.dta", replace
*/
* Import full list of candidates from NIH Reporter - Jan 2026 *;
import delim using "$raw/raw source data/heal_awards_reporter_01152025_sn.csv", delimiters(",") varnames(1) bindquotes(strict) case(preserve) maxquotedrows(30) stringcols(_all) clear /*n=5079*/
capture drop heal_funded
duplicates drop /*n=0 duplicates*/
gen dqauditwave=2

	* Use same date format *;
	/*from 7/31/2025 to 2025-07-31*/
	/* Note: awd_not_date already correct format */
	foreach var in proj_end_date proj_strt_date bgt_strt bgt_end {
		split `var', p("/")
		rename `var' z`var'
			forv i=1/2 {
				gen z`var'`i'="0"+`var'`i'
				replace z`var'`i'=substr(z`var'`i',-2,2)
				drop `var'`i'
				rename z`var'`i' `var'`i'
				}
		egen `var'=concat(`var'3 `var'1 `var'2), punct("-")
		order `var', after(z`var')
		drop z`var' `var'1 `var'2 `var'3
		}

save "$raw/heal_awards_by_serial_number_$today.dta", replace

* Append existing reporter_dqaudit table *;
import delim using "$raw/xold/reporter_dqaudit_2026-01-26.csv", varnames(1) stringcols(_all) bindquote(strict) favorstrfixed clear /*n=448*/
gen dqauditwave=1
append using "$raw/heal_awards_by_serial_number_$today.dta" /*n=5527*/

* Remove duplicate appl_ids *;
duplicates drop /*n=0 dropped*/
sort appl_id dqauditwave
by appl_id: gen count=_n
by appl_id: egen num_rows=max(count)
drop if num_rows==2 & dqauditwave==1 /*n=448 dropped*/
duplicates list appl_id /*n=0 duplicates*/

drop count num_rows dqauditwave
save "$temp/heal_awards_by_serial_number_$today.dta", replace /*n=5079*/



/* ----- 4. Flag appls that are in the reporter table ----- */
use "$temp/heal_awards_by_serial_number_$today.dta", clear
sort appl_id
merge 1:1 appl_id using "$temp/mysql_applkey.dta" /* Note : there are 6 _merge==2 appl_ids, in MySQL that don't appear in the NIH Reporter extract. These are the 6 known problematic appl_ids that don't have a correctly formatted project number. */
drop if _merge==2

* Gen vars *;
gen in_mysql=0
replace in_mysql=1 if _merge==3
drop _merge

	* Dates *;
	foreach var in awd_not_date bgt_end bgt_strt proj_end_date proj_strt_date {
	gen x`var'=substr(`var',1,10)
	gen `var'_date=date(x`var',"YMD")
	format `var'_date %td
	drop x`var'
	order `var'_date,after(`var')
	label var `var'_date "Stata date format"
	}
	
	destring fisc_yr, replace
	gen award_year=year(awd_not_date_date)
		order award_year, after(awd_not_date_date)
		replace award_year=fisc_yr if award_year==. /*n=44 changes made*/

drop in_rep_table_sernum heal_funded res_net in_resnet
		

		
/* ----- 5. Isolate awards we should track that aren't yet in MySQL ----- */
		
* Exclude awards issued before the HEAL Initiative started giving awards *;
drop if award_year<2018 /*n=900 deleted*/

* Apply study ID creation rules *;
sort $stewards_id_vars
egen xstudy_id=group($stewards_id_vars), missing
order xstudy_id appl_id in_mysql award_year $stewards_id_vars

* Drop if awards associated with the study ID are *always* or *never* in MySQL *;
bysort xstudy_id: egen ever_in_mysql=max(in_mysql)
bysort xstudy_id: egen all_in_mysql=min(in_mysql)
drop if ever_in_mysql==0 /*No appl"s associated with the study were in MySQL. n=1059 deleted*/
drop if all_in_mysql==1 /*Every possible appl already in MySQL. n=1654 deleted*/
drop ever_in_mysql all_in_mysql

* Exclude all awards for the study predating an award already in MySQL *;
sort xstudy_id in_mysql award_year
by xstudy_id in_mysql: egen xearliest_award_inmysql=min(award_year)
replace xearliest_award_inmysql=. if in_mysql==0
by xstudy_id: egen earliest_award_inmysql=min(xearliest_award_inmysql)
drop if in_mysql==0 & award_year<earliest_award_inmysql /*n=198*/
drop xearliest_award_inmysql earliest_award_inmysql

	tab in_mysql /* 779 appls in MySQL ;  489 not in MySQL */

rename xstudy_id study
sort study award_year
save "$temp/newer_appls_related_to_studies.dta", replace
export excel using "$temp/newer_appls_related_to_studies.xlsx", firstrow(variables) replace /*n=1268*/ 



/* ----- 5. Rename vars to same names used in reporter table ----- */
/* Note: Did not need renaming in Jan 2026 pull */
/*
* -- Key of var names -- *;
import excel using "$doc/MySQL Database Tables Data Dictionary_April 2024.xlsx", sheet(reporter) firstrow allstring clear
drop L-AE
split var_note, p(";")
rename var_note2 name
rename var_name newname
replace name=subinstr(name,".","",.)
replace name=substr(name,1,32)
keep newname name
drop if newname==name
drop if name==""

save "$doc/varlist_key_matched.dta", replace


* -- Rename vars-- *;
use "$doc/varlist_key_matched.dta", clear
keep name newname	
/*drop if dropvar==1*/
count
local nobs = r(N)
forvalues i = 1/`nobs' {
    local name`i' = name[`i']
	local newname`i' = newname[`i']
	}
	
use "$temp/appls_related_to_studies.dta", clear
forvalues i = 1/`nobs' {
    rename `name`i'' `newname`i''
	}
	save "$temp/appls_related_to_studies_renamed.dta", replace
*/


/* ----- 6. Format and output table for MySQL ----- */
use "$temp/newer_appls_related_to_studies.dta", clear

* Drop records already in mysql *;
drop if in_mysql==1 /*n=489 left */

drop study award_year in_mysql /*heal_funded res_net in_resnet data_check project_start_year organizationorg_ueis organizationprimary_duns organizationprimary_uei opportunity_number */

order appl_id, after(fund_ic_tot_cst)
order proj_ser_num, after(proj_nm_spl_yr)
order proj_num_spl_sfx_code, after(proj_ser_nm_spl)
order subproj_id, after(spd_cat_0)

save "$der/reporter_dqaudit.dta", replace


drop awd_not_date_date bgt_end_date bgt_strt_date proj_end_date_date proj_strt_date_date
export delimited using "$der/reporter_dqaudit.csv", nolab /*quote */replace /*n=489*/
export excel using "$der/reporter_dqaudit.xlsx", firstrow(var) nolabel replace





/* ----- 7. Debug: split up large file into 2 for MySQL import ----- */
use "$der/reporter_dqaudit.dta", clear
drop awd_not_date_date bgt_end_date bgt_strt_date proj_end_date_date proj_strt_date_date
gen row=_n
save "$temp/splits.dta", replace

keep if row<=240
drop row
save "$der/reporter_dqaudit_pt1.dta", replace
export delimited using "$der/reporter_dqaudit_pt1.csv", nolab quote replace

use "$temp/splits.dta", clear
keep if row>240
drop row
save "$der/reporter_dqaudit_pt2.dta", replace
export delimited using "$der/reporter_dqaudit_pt2.csv", nolab quote replace


