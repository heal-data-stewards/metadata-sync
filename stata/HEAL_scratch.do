/* -------------------------------------------------------------------------------- */
/* Project: HEAL 																	*/
/* PI: Kira Bradford																*/
/* Program: HEAL_scratch															*/
/* Programmer: Sabrina McCutchan (CDMS)												*/
/* Date Created: 2024/05/13															*/
/* Date Last Updated: 2025/02/20													*/
/* Description:	This program performs ad-hoc queries.								*/
/*																					*/
/* Notes:  																			*/
/*		- 2024/06/11 reversed order of queries so new queries are added to top of	*/
/*					 program.														*/
/*																					*/
/* -------------------------------------------------------------------------------- */


clear all 


/* ---------------------- */
/* ------- QUERY -------- */
/* ---------------------- */

/* ----- Query: 2025/10/27	----- */
/* Note 1: Tabulate how many consults in Y5 had never used a repository before*/

* --- 1. Find "studies" impacted --- *;
import delimited using "$cal\event-data-from-20250201-to-20251027.csv", varnames(1) case(lower) emptylines(skip) bindquotes(strict) stringcols(_all) clear

* drop test records *;
drop if inlist(cancellationreason,"TEST","test complete")
drop if inlist(response1,"TEST")

* collapse Giorgio Bonmassar records from 4 to 1. "Giorgio Bonmassar will have a few entries listed - he had his consult moved a bit due to conflicts on his side" *;
drop if inviteename=="Giorgio Bonmassar" & eventtypename=="Metadata Consults"

* Derive past repo experience *;
forv i = 1/2 {
	gen used_repo_quest`i'=regexm(question`i', "repository before")
	}
gen asked_repo_use=used_repo_quest1+used_repo_quest2
tab asked_repo_use
sort eventcreateddatetime /* note: we started asking in May 2025*/


gen used_repo_before=.
forv i = 1/2 {
	replace used_repo_before=1 if used_repo_quest`i'==1 & response`i'=="Yes"
	replace used_repo_before=0 if used_repo_quest`i'==1 & response`i'=="No"
	}


sort eventtypename



/* ----- Query: 2025/03/03 & 2025/02/20	----- */
/* Note 1: Check that NIH gave us information about ALL awards associated with HEAL studies, taking FY24 as an example.*/
/* Note 2: The file "$raw/xold/heal_awards_by_serial_number_02142025.csv" was generated by Maria Davila. She took every unique project serial number that existed in the MySQL reporter and awards tables (these table n's were identical at time of query) and fetched every appl_id associated with those serial numbers from NIH Reporter. */

* --- 1. Find "studies" impacted --- *;

* Prep study lookup table *;
use "$temp/nihtables_$today.dta", clear
keep appl_id heal_funded
merge 1:1 appl_id using "$raw/research_networks_$today.dta"
drop if _merge==2
duplicates drop
gen in_resnet=0
replace in_resnet=1 if strtrim(res_net)!=""
keep appl_id heal_funded res_net in_resnet
save "$temp/mysql_applkey.dta", replace

* Import full list of candidates from NIH Reporter *;
import delim using "$raw/xold/heal_awards_by_serial_number_02142025.csv", delimiters(",") varnames(1) bindquotes(strict) case(preserve) maxquotedrows(30) stringcols(_all) clear
drop heal_funded
save "$raw/xold/heal_awards_by_serial_number_02142025.dta", replace

* Merge NIH Reporter candidates with key of appl's already in MySQL *;
use "$raw/xold/heal_awards_by_serial_number_02142025.dta", clear
sort appl_id
merge 1:1 appl_id using "$temp/mysql_applkey.dta" /* Note: there are 6 appl_ids from MySQL that don't appear in the NIH Reporter extract. These are the 6 known problematic appl_ids that don't have a correctly formatted project number. */
drop if _merge==2

* Gen vars *;
gen in_mysql=0
replace in_mysql=1 if _merge==3
drop _merge

	* Dates *;
	foreach var in award_notice_date project_end_date {
	gen x`var'=substr(`var',1,10)
	gen `var'_date=date(x`var',"YMD")
	format `var'_date %td
	drop x`var'
	order `var'_date,after(`var')
	label var `var'_date "Stata date format"
	}
	
	destring budget_year_end, replace
	destring fiscal_year, replace
	gen award_year=year(award_notice_date_date)
		order award_year, after(award_notice_date_date)

* Exclude awards issued before the HEAL Initiative started giving awards *;
drop if award_year<2018 & award_year!=. /*n=877 deleted*/

* Apply study ID creation rules *;
sort project_serial_num project_num_splitsuffix_code subproject_id
egen xstudy_id=group(project_serial_num project_num_splitsuffix_code subproject_id), missing
order xstudy_id appl_id in_mysql award_year project_serial_num project_num_splitsuffix_code subproject_id

* Zero in on records that might need to be imported into MySQL *;
bysort xstudy_id: egen ever_in_mysql=max(in_mysql)
bysort xstudy_id: egen all_in_mysql=min(in_mysql)
drop if ever_in_mysql==0 /*No appl"s associated with the study were in MySQL. n=981 deleted*/
drop if all_in_mysql==1 /*Every possible appl already in MySQL. n=1328 deleted*/
drop ever_in_mysql all_in_mysql

* 2025/03/20 addition: exclude awards older than an award we have in MySQL *;
sort xstudy_id in_mysql award_year
by xstudy_id in_mysql: egen xearliest_award_inmysql=min(award_year)
replace xearliest_award_inmysql=. if in_mysql==0
by xstudy_id: egen earliest_award_inmysql=min(xearliest_award_inmysql)
drop if in_mysql==0 & award_year<earliest_award_inmysql /*n=159*/
drop xearliest_award_inmysql earliest_award_inmysql

	tab in_mysql /* 681 appls in MySQL ;  453 not in MySQL */
	tab in_mysql in_resnet, miss

rename xstudy_id study
sort study award_year
save "$temp/appls_related_to_studies.dta", replace
export excel using "$temp/newer_appls_related_to_studies.xlsx", firstrow(variables) replace /* Note: sent to Maria 2/20/2025 */



* --- 2. Experiment with filtering criteria --- *;

* Exclude "studies" with a research network (only look at independent studies) *;
use "$temp/appls_related_to_studies.dta", clear
sort study in_resnet
by study: egen max_inrn=max(in_resnet)
drop if max_inrn==1 /*n=692 deleted*/
drop max_inrn


* Include appl's awarded *after* an appl_id already in MySQL (sole use case for more appl_ids is most current information) *;
sort study in_mysql award_year
replace award_year=fiscal_year if award_year==. /* Note: for these obsv, all the other date vars like proj start date are also empty*/
by study in_mysql: egen xlast_yr_tracked=max(award_year)
	replace xlast_yr_tracked=. if in_mysql==0
by study: egen last_yr_tracked=max(xlast_yr_tracked)
gen add_to_sql=0
replace add_to_sql=1 if in_mysql==0 & award_year>=last_yr_tracked

keep if add_to_sql==1
save "$temp/add_appls_$today.dta", replace /*n=360*/

/*
* Examples *;
1. Example for Sarah & Gabi: project_num_splitserial_num=="067593". There is a 2023 award, the latest of the batch, not in MySQL, and it has the latest project end date of all awards. 
2. Example of independent study: project_num_splitserial_num=="097880". Some weird award types (2 and 7) that indicate they completed a 5-year project 2017-2022 and then put in the type 2 for a "renewal" from 2023-2028. "Type 2â€”Renewal. Requests an additional funded project period after the current award. Type 2 applications compete with other applications in peer review for funding."
*/


/* ----- Query: 2024/12/17	----- */
/* Note: Discussed w/ Sarah over Zoom chat during previous Fri's MySQL meet to add xstudy_id and res_net flag to the extract Kiryl pulled her */

* Prep study lookup table *;
use "$der/study_lookup_table", clear
sort appl_id study_hdp_id
egen compound_key=concat(appl_id study_hdp_id), punct(_)
sort compound_key
save "$temp/study_id_merging.dta", replace


import delimited using "$temp/sarah_report_12122024.csv", varnames(1) case(lower) bindquotes(strict) stringcols(_all) clear
sort appl_id
foreach var in appl_id hdp_id res_net {
	replace `var'="" if `var'=="NULL"
	replace `var'=subinstr(`var', "`=char(10)'", "`=char(32)'", .) 
		replace `var'=strtrim(`var')
		replace `var'=stritrim(`var')
		replace `var'=ustrtrim(`var')
	}
sort appl_id hdp_id
egen compound_key=concat(appl_id hdp_id), punct(_)
sort compound_key
merge 1:1 compound_key using "$temp/study_id_merging.dta", keepusing(xstudy_id)
drop if _merge==2
drop _merge compound_key
export delimited "$temp/sarah_report_12172024.csv", quote replace




/* ----- Query: 2024/12/04	----- */
/* Note: Gabi over email requested "a list of research network studies expiring in 2025". */

* Get 1 value of res_net for each xstudy_id *;
use "$der/study_lookup_table.dta", clear
sort appl_id
merge m:1 appl_id using "$der/research_networks.dta"
drop if _merge==2
drop _merge res_net_override_flag
keep xstudy_id res_net
sort xstudy_id res_net
duplicates drop /*n=1419*/
	* check uniqueness *;
	egen xgroups=group(xstudy_id res_net), miss
	by xstudy_id: egen max_xgroups=max(xgroups)
	gen xnon_unique_study=1 if max_xgroups!=xgroups
	by xstudy_id: egen non_unique_study=max(xnon_unique_study)
	browse if non_unique_study==1 /* Note; 12/4/24, there are never 2 diff values of res_net for the same study_id, only one missing and one non-missing value of res_net */
	drop if non_unique_study==1 & res_net==""
keep xstudy_id res_net
rename res_net study_res_net
duplicates list xstudy_id
save "$temp/study_res_net_key.dta", replace
	

* Prep study info *;
use "$der/study_lookup_table.dta", clear
destring xstudy_id, generate(study_id_final)
sort xstudy_id
merge m:1 xstudy_id using "$temp/study_res_net_key.dta"
drop _merge
sort appl_id
merge m:1 appl_id using "$der/engagement_flags.dta"
drop _merge xstudy_id study_hdp_id_appl
order study_id_final
sort study_id_final study_most_recent_appl study_hdp_id 
drop appl_id
duplicates drop 
save "$temp/study_info.dta", replace /*n=1415*/

* Prep full dataset for appl_ids belonging to a study */
use "$der/mysql_$today.dta", clear /*n=1988*/
drop if mds_ctn_flag==1  /*n=40 dropped */
drop if proj_ser_num=="" /*n=6 dropped*/
egen compound_key=concat(appl_id hdp_id), punct(_)
/*
* Merge res_net *;
sort appl_id
merge m:1 appl_id using "$der/research_networks.dta"
drop if _merge==2
drop if res_net=="CTN"  /*n=1750*/
drop res_net_override_flag _merge 
*/
* Merge study info *;
merge 1:1 compound_key using "$doc/studyidkey.dta", keepusing(study_id_final)
drop _merge
merge m:1 study_id_final using "$temp/study_info.dta"
drop _merge /*n=1942*/

* Exclusion criteria *;
drop if entity_type!="Study" /*n=0 dropped*/
drop if study_res_net=="" /*n=1199 dropped*/
drop if merge_awards_mds==2 /*n=0 dropped*/
drop if do_not_engage==1 /*n=0*/
	* N=557 remaining *;
/*	
* Expiring in 2025 *;
gen year_end=year(proj_end_date_date)
keep if year_end==2025 /*n=45 left*/
*/


* Latest appl_id for project ends in 2025 *;
gen year_end=year(proj_end_date_date) if study_most_recent_appl==appl_id /*n=130 missing values */
keep if year_end==2025 /*n=42 left*/

* Output results *;
keep study_id_final year_end study_most_recent_appl study_hdp_id study_res_net

sort study_most_recent_appl study_hdp_id
export delimited using "$out/resnets_ending_2025.csv", quote replace






/* ----- Query: 2024/10/11	----- */
/* Note: RJ email a list of 6 HDP IDs and asked "Are these all multi years that had phases end in our current target years? ('23 and '24) " */

* Flag study ids for the given HDP IDs *;
use "$der/study_lookup_table.dta", clear
keep if inlist(study_hdp_id,"HDP00283","HDP00264","HDP00289","HDP00517","HDP00143","HDP00593")
destring xstudy_id, generate(study_id_final)
drop xstudy_id appl_id
duplicates drop
gen keep_study_ids=1
sort study_id_final
save "$temp/keep_study_ids.dta", replace

* Pull all appl_ids records associated with the named HDP IDs *;
use "$der/mysql_$today.dta", clear /*n=1719*/
egen compound_key=concat(appl_id hdp_id), punct(_)
sort appl_id
merge 1:1 compound_key using "$doc/studyidkey.dta", keepusing(study_id_final)
drop _merge
sort study_id_final
merge m:1 study_id_final using "$temp/keep_study_ids.dta"
drop _merge
keep if keep_study_ids==1

gen year_end=year(proj_end_date_date)

keep appl_id hdp_id proj_end_date_date year_end study_*
rename study_id_final xstudy_id
drop study_name

order appl_id proj_end_date_date year_end hdp_id

export delimited using "$out/hdpid_query_2024-10-11.csv", quote replace




/* ----- Query: 2024/10/01	----- */
/* Note: RJ over email requested "a list of independent studies and SBIR's expiring in 2025". */

* Prep study info *;
use "$der/study_lookup_table.dta", clear
drop appl_id
destring xstudy_id, generate(study_id_final)
drop xstudy_id
order study_id_final
sort study_id_final study_most_recent_appl study_hdp_id 
duplicates drop 
save "$temp/study_info.dta", replace /*n=1214*/


* Merge res_net *;
use "$der/mysql_$today.dta", clear /*n=1719*/
egen compound_key=concat(appl_id hdp_id), punct(_)
sort appl_id
merge m:1 appl_id using "$der/research_networks.dta"
drop _merge
merge 1:1 compound_key using "$doc/studyidkey.dta", keepusing(study_id_final)
drop _merge
merge m:1 study_id_final using "$temp/study_info.dta"
drop _merge

* Exclusion criteria *;
drop if entity_type!="Study" /*n=46 dropped*/
drop if merge_awards_mds==2 /*n=2 dropped*/
keep if res_net=="" /*n=739 dropped*/

* Expiring in 2025 *;
gen year_end=year(proj_end_date_date)
keep if year_end==2025 /*n=133 left*/

* Output results *;
keep appl_id hdp_id fisc_yr fund_mech year_end study_id_final study_id_final study_hdp_id study_hdp_id_appl merge_awards_mds

	browse if hdp_id=="" /* Note: these do make it into study_lookup_table */

sort fund_mech appl_id hdp_id
export delimited using "$out/appls_ending_2025.csv", quote replace




/* ----- Query: 2024/09/10	----- */
/* Note: Request from Kathy: Identify how many HEAL studies are funded by NIMH */
use "$der/study_lookup_table.dta", clear
keep appl_id study_hdp_id xstudy_id
sort appl_id study_hdp_id
duplicates drop
save "$temp/study_ids_formerge.dta", replace /*n=1481*/

* Add study ID to full dataset *;
use "$der/mysql_$today.dta", clear
order appl_id $stewards_id_vars hdp_id
sort $stewards_id_vars appl_id hdp_id	

	* Exclude records of non-study entities (CTN & "other") *;
	drop if mds_ctn_flag==1  /*n=40 dropped */
	drop if proj_ser_num=="" /*n=6 dropped*/
	sort appl_id
	merge m:1 appl_id using "$der/research_networks.dta"
	drop if _merge==2
	drop _merge res_net_override_flag
	drop if res_net==4 /*n=1481 deleted*/

sort appl_id hdp_id
/*by appl_id: gen xnum_rows_for_applid=_n
by appl_id: egen num_rows_for_applid=max(xnum_rows_for_applid)
drop xnum_rows_for_applid*/
merge m:m appl_id using "$temp/study_ids_formerge.dta" /*n=1481*/
save "$temp/studies_NDA_affiliation.dta", replace
	/* m:m merge is done because the hdp_id from using is attached to the xstudy_id, not the appl_id, and trying to use it as a merge var messes up the merge. The m:m merge command produces the desired result of appending the correct xstudy_id to the master data, though I'm not entirely sure how it chooses the correct hdp_id + studyid from using to match to master (but it does!) */

* Find IC for each study ID *;
/* temp override adm_ic for the 1 appl_id that is in MDS but not MySQL yet */
use "$temp/studies_NDA_affiliation.dta", clear
keep appl_id hdp_id xstudy_id adm_ic fund_ic 
replace adm_ic="NIDA" if appl_id=="10875930"
keep adm_ic xstudy_id
duplicates drop
tab adm_ic

* Find repository selection *;
use "$temp/studies_NDA_affiliation.dta", clear
keep appl_id hdp_id xstudy_id adm_ic repository_name 
/*duplicates list xstudy_id*/
keep if repository_name!=""
duplicates list xstudy_id
tab repository_name

* Check DAI-2 for repository selection *;
global dai "C:\Users\smccutchan\OneDrive - Research Triangle Institute\Documents\HEAL\DAI2\Derived"
use "$dai\dai2_clean.dta", clear
keep if heal_dai2_assessment_complete==2




/* ----- Query: 2024/07/25	----- */
/* Note: Request from Anthony and arbitrated by RJ and Maria: Pull a list of all HEAL-funded appl_IDs currently in the MySQL DB */
use "$der/xold/mysql_2024-07-18.dta", clear
drop if appl_id==""
drop if merge_awards_mds==2 /* Limit to what's in MYSQL; exclude awards that're only in MDS*/
drop if heal_funded=="N" /* Exclude appl_ids that aren't HEAL funded */
keep appl_id heal_funded proj_title proj_num_spl_ty_code rfa res_prg
replace heal_funded="?" if heal_funded==""
replace heal_funded="Yes" if heal_funded=="Y"
sort heal_funded appl_id
label var appl_id "Application ID"
label var heal_funded "[awards] Award was HEAL-funded"
label var proj_title "[reporter] Project Title"
label var proj_num_spl_ty_code "[reporter] Project Type Code"
label var rfa "[awards] Research Focus Area"
label var res_prg "[awards] Research Program"
export delimited using "$out/HEAL_MySQL_applids_2024-07-31.csv", quote replace




/* ----- Query: 2024/07/01	----- */
/* Note: Slack from KathyJ in #mysql-updates on 2024/06/28
Could someone with access to MySQL extract everything about all HEAL studies with PI Yong Chen for me? Title is "Modeling temporomandibular joint disorders pain: role of transient receptor potential ion channels". Looks like they got 7 different NIH awards. The engagement team might be able to clear up the study list for us. Thanks!
*/
use "$der/mysql_$today.dta", clear
foreach x of varlist pi pi_fst_nm pi_lst_nm {
	replace `x'=lower(`x')
	replace `x'=subinstr(`x', "`=char(10)'", "`=char(32)'", .) /* replace linebreaks inside cells with a space */
	replace `x'=strtrim(`x')
	replace `x'=stritrim(`x')
	replace `x'=ustrtrim(`x')
	split `x', p(";")
	}
gen xname_flag=0
forv i=1/7 {
	replace xname_flag=1 if pi_lst_nm`i'=="chen"
	/*replace xname_flag=1 if pi_lst_nm`i'=="yong"   no results */
	}
browse if xname_flag==1
gen name_flag=0
forv i=1/7 {
	replace name_flag=1 if pi`i'=="yong chen"
	}
keep if name_flag==1
forv i=1/7 {
	drop pi`i' pi_fst_nm`i' pi_lst_nm`i'
	}
drop name_flag xname_flag
save "$out/yongchen_$today.dta", replace
export excel using "$out/yongchen_$today", replace firstrow(var) keepcellfmt





/*
/* ----- Query: 2024/05/13	----- */
/* Note: Slack from KathyJ in #mysql-updates on 2024/05/13
I investigated the list of 14 appl_IDs above which are in the Studies board but I didn't find in the lookup table. Several seem to be legit HEAL studies based on Reporter data. For the ones below in particular, when you get a chance, it would be good to 1) check if they are in MySQL and 2) confirm whether they are or are not in the lookup table. If they are in MySQL and not in the lookup table, we can try to figure out why.
9673173  (5U24HD095254-02)
9769689  (5R01DE027454-02)
10593312  (3R24DA055306-02S1)
*/

use "$raw/reporter_$today.dta", clear
browse if inlist(appl_id,"9673173","9769689","10593312")


use "$raw/awards_$today.dta", clear
browse if inlist(appl_id,"9673173","9769689","10593312")

import excel using "$doc/Change Log.xlsx", sheet("Modifications") firstrow clear
browse if inlist(ApplID,"9673173","9769689","10593312")




/* ----- Query: 2024/05/28	----- */
/* Note: Pull record for consult prep */
use "$der/mysql_$today.dta", clear
keep if hdp_id=="HDP00014"
export delimited using "$out/reike.csv"




/* ----- Query: 2024/05/31	----- */
use "$out/study_lookup_table.dta", clear
rename study_hdp_id hdp_id
sort hdp_id
merge m:1 hdp_id using "$raw/progress_tracker_$today.dta", keepusing(archived)
drop if _merge==2
drop _merge
gen most_recent_appl_archived=0
keep if appl_id==study_most_recent_appl /*n=1313*/
replace most_recent_appl_archived=1 if archived=="archived"
export excel using "$out/most_recent_appls_by_archived_status", replace firstrow(var) keepcellfmt




/* ----- Check MySQL Updates: 2024/06/06	----- */
use "$raw/reporter_$today.dta", clear
order appl_id
drop if appl_id==""
merge 1:1 appl_id using "$raw/xold/reporter_2024-05-31.dta"
keep if _merge==1
save "$temp/new_reporter_rows_$today.dta", replace
